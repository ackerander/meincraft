#version 330 core

layout(location = 0) in uint vertI;
layout(location = 1) in ivec3 offset;
layout(location = 2) in uvec2 span;
layout(location = 3) in uint face;
layout(location = 4) in int tex;

out vec2 UV;
out vec2 texCoord;
out float light;
uniform mat4 MVP;

const vec2 points[4] = vec2[](vec2(0, 1), vec2(0, 0), vec2(1, 1), vec2(1, 0));
const mat2x3 rots[6] = mat2x3[](mat2x3(1, 0, 0, 0, 1, 0), mat2x3(0, 0, -1, 0, 1, 0),
	mat2x3(-1, 0, 0, 0, 1, 0), mat2x3(0, 0, 1, 0, 1, 0),
	mat2x3(1, 0, 0, 0, 0, -1), mat2x3(1, 0, 0, 0, 0, 1));
const float ilum[6] = float[](0.6, 0.3, 0.5, 0.75, 0.95, 0.15);

void main()
{
	vec2 pt = span * points[vertI];
	gl_Position =  MVP * vec4(rots[face] * pt + offset, 1);
	UV = pt;
	light = ilum[face];
	texCoord = 0.0625 * vec2(tex & 0xF, tex >> 4);
}
